allprojects {
    apply plugin: "eclipse-wtp"
    apply plugin: "idea"
}

//common for both projects
subprojects {
    repositories {
        mavenCentral()
    }

    ext.baseNameUpper = project.parent.name.capitalize()
    ext.baseNameLower = project.parent.name.toLowerCase()

    apply plugin: "java"

    apply from: "http://plugins.jasoft.fi/vaadin.plugin"

    vaadin.version="7.4.6"

    vaadin.gwt.draftCompile = true
    vaadin.gwt.strict = true
    vaadin.manageWidgetset = false
    vaadin.plugin.logToConsole = true


    vaadin.addon.author = "Risto Yrjänä / Vaadin"
    vaadin.addon.license = "Apache License 2.0"
    vaadin.addon.title = baseNameUpper

    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"

    group = "org.vaadin.risto.${baseNameLower}"

    //creates archives named "name-version.jar" and "name.war" where name is the name of the main project
    archivesBaseName = baseNameLower
}

//addon project, only creates the Directory-compatible JAR
project(":addon") {
    eclipse.project.name = "${baseNameUpper}-addon"

    version = "2.3.0"
    vaadin.widgetset = "${group}.widgetset.${baseNameUpper}Widgetset"
    vaadin.addon.styles = ["/VAADIN/addons/stepper/stepper.scss"]

    dependencies {
        testCompile "junit:junit:latest.release"
    }

    jar {
        from sourceSets.main.allJava //include sources
    }

    war.enabled = false //war plugin applied by the Vaadin plugin, not needed here
}

//demo project, builds addon and packages demo UI as WAR
project(":demo") {

    eclipse.project.name = "${baseNameUpper}-demo"

    dependencies {
        compile project(":addon")
        compile "com.google.guava:guava:latest.release"
    }

    vaadin.widgetset = "${group}.widgetset.${baseNameUpper}DemoWidgetset"

    vaadin.devmode.superDevMode = true
}

task(dist, type: Sync, dependsOn: [':addon:jar', ':demo:war']) {
    description = "Copies the addon JAR and the demo WAR to target/"
    File targetDir = mkdir("target")
    from project(":addon").jar.archivePath
    from project(":demo").war.archivePath
    into targetDir
}
