//common for both projects
subprojects {
	repositories {
    	mavenCentral()
	}

	apply plugin: "java"

	apply from: "/Users/ripla/Documents/workspaces/community/gradle-vaadin-plugin/vaadin-development.plugin"
	vaadin.version = "7.0.0.rc2"

	sourceCompatibility = "1.6"
	targetCompatibility = "1.6"

	ext.baseNameUpper = project.parent.name.capitalize()
	ext.baseNameLower = project.parent.name.toLowerCase()
	
	group = "org.vaadin.risto.${baseNameLower}"

	//creates archives named "name-version.jar" and "name.war" where name is the name of the main project
	archivesBaseName = baseNameLower
}

//addon project, only creates the Directory-compatible JAR
project(":addon") {
	apply plugin: "eclipse"
	eclipse.project.name = "${baseNameUpper}-addon"

	version = "2.0.2"
	vaadin.widgetset = "${group}.widgetset.${baseNameUpper}Widgetset"
	vaadin.manageWidgetset = false

	dependencies {
		testCompile "junit:junit:4.+"
	}

	jar {
		from sourceSets.main.allJava //include sources
	   
		manifest = project.manifest {
			attributes 'Implementation-Title': "${baseNameUpper}"
			attributes 'Implementation-Version': version
			attributes 'Implementation-Vendor': 'Risto Yrjänä / Vaadin'
			attributes 'Vaadin-Package-Version': '1'
			attributes 'Vaadin-License-Title': 'Apache License 2.0'
			attributes 'Vaadin-Widgetsets': vaadin.widgetset
			}
		}
		
	war.enabled = false //war plugin applied by the Vaadin plugin, not needed here
}

//demo project, builds addon and packages demo UI as WAR
project(":demo") {
	apply plugin: "eclipse-wtp"

	eclipse.project.name = "${baseNameUpper}-demo"
	
	dependencies {
		compile project(":addon")
		compile "com.google.guava:guava:14.0-rc2"
	}

	vaadin.widgetset = "${group}.widgetset.${baseNameUpper}DemoWidgetset"
	vaadin.manageWidgetset = false
}

task(dist, type: Sync, dependsOn: [':addon:build', ':demo:build']) {
	description = "Copies the addon JAR and the demo WAR to target/"
	File targetDir = mkdir("target")
	from project(":addon").jar.archivePath
	from project(":demo").war.archivePath
	into targetDir
}